// http://colobot.info/SatComOnline/master/cbot/category
// find titanium
// carry to factory
// drop
// start creating WingedShooter
extern void object::ft_to_f_cr_WiSh()
{
	errmode(0);
	
	object t;
	
	if(energyCell.energyLevel < 0.3)
	{
		recharge();
	}
	
	t = radar(Titanium, 0, 360, 0, 1000);
	
	if( t == null)
	{
		message("Titanium not found");
		return;
	}
	
	move_to(t);
	
	grab();
	
	t = radar(BotFactory);
	
	move_to(t);
	
	drop();
	
	move(-10);
	
	t.factory(WingedShooter);
}

void object::recharge()
{
	object t;
	
	t = radar(PowerStation);
	move_to(t);
	
	while ( energyCell.energyLevel < 1 )
	{
		wait(1);
	}
}

void object::move_to(object o)
{
	int errcode  = 0;
	errcode = goto(o.position);
	
	while(errcode != 0)
	{
		errcode = goto(o.position, 20, 0, 1);
	}
}

